cmake_minimum_required(VERSION 3.27)

project(ProjectB)

set(PROJECT_NAME "FlappyBird2D")

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR})
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR})
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR})

add_subdirectory(ThirdParty/SDL)
add_subdirectory(ThirdParty/jsoncpp)
add_subdirectory(ThirdParty/glad)
add_subdirectory(ThirdParty/miniaudio)
add_subdirectory(ThirdParty/stb)

add_subdirectory(Module/CrashModule)
add_subdirectory(Module/FileModule)
add_subdirectory(Module/MathModule)

set_target_properties(glad jsoncpp miniaudio SDL2 SDL2main sdl_headers_copy stb PROPERTIES FOLDER "ThirdParty")
set_target_properties(CrashModule FileModule MathModule PROPERTIES FOLDER "Module")

set(WINDOW_TITLE "${PROJECT_NAME}")
set(WINDOW_POS_UNDEFINED 0)
set(WINDOW_POS_CENTERED 1)
set(WINDOW_X 100)
set(WINDOW_Y 100)
set(WINDOW_W 600)
set(WINDOW_H 800)
set(GL_MAJOR_VERSION 4)
set(GL_MINOR_VERSION 6)
set(GL_RED_SIZE 8)
set(GL_GREEN_SIZE 8)
set(GL_BLUE_SIZE 8)
set(GL_ALPHA_SIZE 8)
set(GL_DEPTH_SIZE 24)
set(GL_STENCIL_SIZE 8)
set(GL_DOUBLEBUFFER 1)
set(GL_MULTISAMPLEBUFFERS 1)
set(GL_MULTISAMPLESAMPLES 16)

configure_file(
    "${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_NAME}/Inc/SDLConfig.h.in"
    "${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_NAME}/Inc/SDLConfig.h"
)

set(EXE_ICON_PATH "${CMAKE_CURRENT_SOURCE_DIR}/Resource/Icon/favicon.ico")
configure_file(
    "${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_NAME}/Res/Resource.rc.in"
    "${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_NAME}/Res/Resource.rc"
)

set(PROJECT_INCLUDE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_NAME}/Inc")
set(PROJECT_SOURCE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_NAME}/Src")
set(PROJECT_RESOURCE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_NAME}/Res")
set(SCRIPT_PATH "${CMAKE_CURRENT_SOURCE_DIR}/Script")
set(SHADER_PATH "${CMAKE_CURRENT_SOURCE_DIR}/Shader")

file(GLOB_RECURSE PROJECT_INCLUDE_FILE "${PROJECT_INCLUDE_PATH}/**")
file(GLOB_RECURSE PROJECT_SOURCE_FILE "${PROJECT_SOURCE_PATH}/**")
file(GLOB_RECURSE PROJECT_RESOURCE_FILE "${PROJECT_RESOURCE_PATH}/**")
file(GLOB_RECURSE SCRIPT_SOURCE_FILE "${SCRIPT_PATH}/*")
file(GLOB_RECURSE SHADER_SOURCE_FILE "${SHADER_PATH}/*")

add_executable(
    ${PROJECT_NAME} 
    WIN32 
    ${PROJECT_INCLUDE_FILE}
    ${PROJECT_SOURCE_FILE} 
    ${PROJECT_RESOURCE_FILE}
    ${SCRIPT_SOURCE_FILE} 
    ${SHADER_SOURCE_FILE}
)

target_include_directories(
    ${PROJECT_NAME} 
    PUBLIC 
    ${PROJECT_INCLUDE_PATH} 
    ${PROJECT_SOURCE_PATH}
    ${PROJECT_RESOURCE_PATH}
)

target_link_libraries(
    ${PROJECT_NAME} 
    PUBLIC 
    glad
    jsoncpp
    miniaudio
    SDL2
    stb 
    CrashModule
    FileModule
    MathModule
)

target_compile_definitions(
    ${PROJECT_NAME} 
    PUBLIC
    $<$<CONFIG:Debug>:DEBUG_MODE>
    $<$<CONFIG:Release>:RELEASE_MODE>
    $<$<CONFIG:RelWithDebInfo>:DEVELOPMENT_MODE>
    $<$<CONFIG:MinSizeRel>:SHIPPING_MODE>
)

set_target_properties(${PROJECT_NAME} PROPERTIES LINK_FLAGS "/level='requireAdministrator' /uiAccess='false'")
set_property(TARGET ${PROJECT_NAME} PROPERTY MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>")
set_property(TARGET ${PROJECT_NAME} PROPERTY VS_DEBUGGER_WORKING_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}")
set_property(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT ${PROJECT_NAME})

source_group(TREE "${PROJECT_INCLUDE_PATH}" PREFIX "${PROJECT_NAME}/Inc" FILES ${PROJECT_INCLUDE_FILE})
source_group(TREE "${PROJECT_SOURCE_PATH}" PREFIX "${PROJECT_NAME}/Src" FILES ${PROJECT_SOURCE_FILE})
source_group(TREE "${PROJECT_RESOURCE_PATH}" PREFIX "${PROJECT_NAME}/Res" FILES ${PROJECT_RESOURCE_FILE})
source_group(Script FILES ${SCRIPT_SOURCE_FILE})
source_group(Shader FILES ${SHADER_SOURCE_FILE})

set(CPACK_GENERATOR "NSIS")
set(CPACK_NSIS_MUI_ICON ${CMAKE_CURRENT_SOURCE_DIR}/Resource/Icon/favicon.ico)
set(CPACK_NSIS_MUI_UNIICON ${CMAKE_CURRENT_SOURCE_DIR}/Resource/Icon/favicon.ico)
set(CPACK_NSIS_MUI_HEADERIMAGE  ${CMAKE_CURRENT_SOURCE_DIR}\\\\Resource\\\\Texture\\\\Header.bmp)
set(CPACK_NSIS_MUI_HEADERIMAGE_UNBITMAP  ${CMAKE_CURRENT_SOURCE_DIR}\\\\Resource\\\\Texture\\\\Header.bmp)
set(CPACK_NSIS_MUI_WELCOMEFINISHPAGE_BITMAP ${CMAKE_CURRENT_SOURCE_DIR}\\\\Resource\\\\Texture\\\\Install.bmp)
set(CPACK_NSIS_MUI_UNWELCOMEFINISHPAGE_BITMAP ${CMAKE_CURRENT_SOURCE_DIR}\\\\Resource\\\\Texture\\\\Install.bmp)

install(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/Shader DESTINATION .)
install(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/Resource DESTINATION .)
install(FILES 
    "$<$<CONFIG:Debug>:${CMAKE_BINARY_DIR}/Debug/SDL2d.dll>"
    "$<$<CONFIG:Release>:${CMAKE_BINARY_DIR}/Release/SDL2.dll>"
    "$<$<CONFIG:RelWithDebInfo>:${CMAKE_BINARY_DIR}/RelWithDebInfo/SDL2.dll>"
    "$<$<CONFIG:MinSizeRel>:${CMAKE_BINARY_DIR}/MinSizeRel/SDL2.dll>"
    DESTINATION .
)
install(TARGETS ${PROJECT_NAME} DESTINATION .)

set(CPACK_PACKAGE_NAME ${PROJECT_NAME})
set(CPACK_PACKAGE_VERSION "0.0.0")
set(CPACK_PACKAGE_VENDOR "Github-ChoiJiOne")
set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "https://github.com/ChoiJiOne/ProjectB_FlappyBird2D")
set(CPACK_PACKAGE_INSTALL_DIRECTORY ${PROJECT_NAME})
set(CPACK_PACKAGE_EXECUTABLES ${PROJECT_NAME} ${PROJECT_NAME})

set(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE.txt")

include(CPack)